module Eratosthenes
exports all
definitions
state Sieve of
    sieve : seq of bool
init s == s = mk_Sieve([])
end
operations
    isPrime : nat1 ==> bool
    isPrime(n) ==
        (if n > len sieve then createSieve(n);
        return sieve(n))
    pre  n >= 2
    post 
        RESULT <=> not (exists p in set {2, ..., n - 1} & n mod p = 0);
    
    createSieve : nat1 ==> ()
    createSieve(n) ==
        (dcl newSieve:seq of bool := [true | i in set {1, ..., n}], i:nat1 := 2;
        newSieve(1) := false;
        while i  * i <= n
        do
            (if newSieve(i) then for m = i  * 2 to  n by  i do newSieve(m) := false;
            i := i + 1);
        sieve := newSieve)
    pre  n >= 2
    post 
        len sieve = n
        and (forall i in set {2, ..., n} &
                sieve(i) <=> not (exists p in set {2, ..., i - 1} & i mod p = 0));
traces
    Singles:
        let n in set {2, ..., 50} in isPrime(n);
    
    Pairs:
        let n1, n2 in set {2, ..., 50}
        in
            (isPrime(n1);
            isPrime(n2));
end Eratosthenes